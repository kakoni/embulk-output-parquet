plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "checkstyle"

    id "org.embulk.embulk-plugins" version "0.7.0"
    id 'tech.yanand.maven-central-publish' version '1.3.0'
}

group = "org.embulk.output"
version = "0.1.0"
description = "An Embulk plugin to output Parquet files to local filesystem."

sourceCompatibility = 8
targetCompatibility = 8
compileJava.options.encoding = 'UTF-8'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

ext {
    hadoopVersion = '3.4.0'
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation("org.embulk:embulk-util-config:0.5.0")

    implementation "org.apache.hadoop:hadoop-common:$hadoopVersion"
    // Needed for Parquet reader builder classes that depend on MR APIs
    implementation "org.apache.hadoop:hadoop-mapreduce-client-core:$hadoopVersion"
    implementation "org.apache.parquet:parquet-avro:1.15.2"

    testImplementation "junit:junit:4.13.2"
    
    // Add Mockito for better testing
    testImplementation "org.mockito:mockito-core:4.11.0"

    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-core:0.11.5"
    testImplementation "org.embulk:embulk-deps:0.11.5"
    testImplementation "org.embulk:embulk-junit4:0.11.5"
}

embulkPlugin {
    mainClass = "org.embulk.output.ParquetOutputPlugin"
    category = "output"
    type = "parquet"
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}
sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}
javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

// The publishing settings are usually required to publish the plugin to Maven Central.
// Publish it by: "./gradlew publishMavenPublicationToMavenRepository"
publishing {
    publications {
        create('maven', MavenPublication)  {
            groupId = project.group
            artifactId = "embulk-output-parquet"
            version = project.version

            from ((SoftwareComponent) components.java)
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            // Some pom.xml attributes are mandatory in Maven Central.
            // See: https://central.sonatype.org/pages/requirements.html
            pom {
                packaging "jar"

                name = "embulk-output-parquet"
                description = project.description
                url = ""

                licenses {
                    license {
                        // See: http://central.sonatype.org/pages/requirements.html#license-information
                        name = "MIT License"
                        url = ""
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Shinichi Hashiba"
                        email = "shinichi.hashiba@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/kakoni/embulk-output-parquet.git"
                    developerConnection = "scm:git:https://github.com/kakoni/embulk-output-parquet.git"
                    url = "https://github.com/kakoni/embulk-output-parquet"
                }
            }
        }
    }

    repositories {
        maven {
            name = "local"
            url = layout.buildDirectory.dir('repos/bundles')
        }
    }
}

mavenCentral {
    repoDir = layout.buildDirectory.dir('repos/bundles')
    authToken = System.getenv("AUTH_TOKEN")
    publishingType = 'AUTOMATIC'
    maxWait = 60
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys((String)signingKey, (String)signingPassword)
    }

//    useInMemoryPgpKeys(System.getenv("PGP_SIGNING_KEY") , System.getenv("PGP_SIGNING_KEY_PASSPHRASE"))
//    required { gradle.taskGraph.hasTask("publishMavenJavaPublicationToMavenRepository") }
    sign publishing.publications.maven
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '10.18.1'
}

checkstyleMain {
    ignoreFailures = true
}
checkstyleTest {
    ignoreFailures = true
}

gem {
    authors = [ "Karri Niemel√§" ]
    email = [ "kakoni@gmail.com" ]
    summary = "parquet output plugin for Embulk"
    homepage = "https://github.com/kakoni/embulk-output-parquet"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}
